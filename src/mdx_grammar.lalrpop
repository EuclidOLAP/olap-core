use crate::mdx_tokens::{Token, LexicalError};

use crate::mdx_ast::AstSeg;
use crate::mdx_ast::{AstFactory, AstTerm, AstExpression};
use crate::mdx_ast::AstFactory::{FactoryNum, FactoryStr, FactorySegs,FactoryTuple,FactoryExp};
use crate::mdx_ast::{AstLevelFunction, AstLevelFnLevel, AstLevelFnLevels};
use crate::mdx_ast::{AstMemberFunction, AstMemberFnParent, AstMemberFnClosingPeriod, AstMemberFnOpeningPeriod};
use crate::mdx_ast::{AstMemberFnCurrentMember};
use crate::mdx_ast::{AstSetFunction, AstSetFnChildren};
use crate::mdx_ast::{AstExpFunction};
use crate::mdx_ast::{AstNumFnCount, AstNumFnAvg};
use crate::mdx_ast::{AstExpFnName};
use crate::mdx_ast::{AstExpFnLookupCube};
use crate::mdx_ast::AstNumFnIIf;
use crate::mdx_ast::AstBoolExp;
use crate::mdx_ast::AstBoolTerm;
use crate::mdx_ast::AstBoolFactory;
use crate::mdx_ast::AstBoolFunction;
use crate::mdx_ast::AstBoolFnIsLeaf;

use crate::exmdx::ast::AstSegsObj;
use crate::exmdx::ast::{AstMdxStatement, AstSet, AstTuple, AstAxis, AstCustomObject};

use crate::exmdx::exp_func::{AstNumFnSum, AstNumFnMax, AstNumFnMin};

// code review mark >>>>>
// code review mark >>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
// code review mark >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

grammar;

extern {
  type Location = usize;
  type Error = LexicalError;

  enum Token {
    "with" => Token::With,
    "member" => Token::Member,
    "set" => Token::Set,
    "as" => Token::As,

    "select" => Token::Select,
    "on" => Token::On,
    "from" => Token::From,
    "where" => Token::Where,

    "Columns" => Token::Columns,
    "Rows" => Token::Rows,
    "Pages" => Token::Pages,
    "Chapters" => Token::Chapters,
    "Sections" => Token::Sections,

    "(" => Token::RoundBracketLeft,
    ")" => Token::RoundBracketRight,
    "{" => Token::CurlyBraceLeft,
    "}" => Token::CurlyBraceRight,

    "," => Token::Comma,
    "&" => Token::Ampersand,
    "." => Token::Dot,
    ";" => Token::Semicolon,

    "+" => Token::Plus,
    "-" => Token::Minus,
    "*" => Token::Multiplied,
    "/" => Token::Divided,

    "<" => Token::LT,
    "<=" => Token::LE,
    "=" => Token::EQ,
    "<>" => Token::NE,
    ">" => Token::GT,
    ">=" => Token::GE,

    "unsigned_int" => Token::Integer(<u64>),
    "double" => Token::Double(<f64>),

    "BracketedString" => Token::BracketedString(<String>),
    "QuotedString" => Token::QuotedString(<String>),

    "Parent" => Token::Parent,
    "CurrentMember" => Token::CurrentMember,
    "ClosingPeriod" => Token::ClosingPeriod,
    "OpeningPeriod" => Token::OpeningPeriod,

    "Children" => Token::Children,

    "Level" => Token::Level,
    "Levels" => Token::Levels,

    "Avg" => Token::Avg,
    "Sum" => Token::Sum,
    "Max" => Token::Max,
    "Min" => Token::Min,
    "Count" => Token::Count,
    // "Tunnel" => Token::Tunnel,
    "LookupCube" => Token::LookupCube,
    "IIf" => Token::IIf,

    "Name" => Token::Name,

    "Not" => Token::Not,
    "Or" => Token::Or,
    "And" => Token::And,

    "IsLeaf" => Token::IsLeaf,
  }
}

// MDX Function Reference
// https://docs.oracle.com/en/database/other-databases/essbase/21/esscq/mdx-function-list.html
// https://learn.microsoft.com/en-us/sql/mdx/mdx-function-reference-mdx?view=sql-server-ver17
// https://www.ibm.com/docs/en/planning-analytics/2.0.0?topic=references-mdx-function-support

pub MdxStatement: AstMdxStatement = {
  <querying: Slicing_Querying> (";")? => {
    AstMdxStatement::new(vec![], querying)
  },
  "with" <custom_objs: With_Custom_Objects> <querying: Slicing_Querying> (";")? => {
    AstMdxStatement::new(custom_objs, querying)
  },
}

Slicing_Querying: (Vec<AstAxis>, AstSegsObj, Option<AstTuple>) = {
  "select" <axes: Axes> "from" <cube_segs: Segs_Obj> => {
    (axes, cube_segs, None)
  },
  "select" <axes: Axes> "from" <cube_segs: Segs_Obj> "where" <ast_tup: Bracket_Tuple> => {
    (axes, cube_segs, Some(ast_tup))
  },
  "select" <axes: Axes> "from" <cube_segs: Segs_Obj> "where" <segs_obj: Segs_Obj> => {
    (axes, cube_segs, Some(AstTuple::SegsObj(segs_obj)))
  },
}

Axes: Vec<AstAxis> = {
  <axis: Axis> => {
    vec![axis]
  },
  <axes: Axes> (",")? <axis: Axis> => {
    let mut list = axes;
    list.push(axis);
    list
  }
}

// COLUMNS | ROWS | PAGES | CHAPTERS | SECTIONS | AXIS (<unsigned_integer>)
Axis: AstAxis = {
  <ast_set: Brace_Set> "on" <position: AxisPos> => { // <crace_set: Brace_Set> <set_segs: Segs_Obj>
    AstAxis { ast_set, position }
  },
  <segs_obj: Segs_Obj> "on" <position: AxisPos> => {
    AstAxis { ast_set: AstSet::SegsObj(segs_obj), position }
  },
}

With_Custom_Objects: Vec<AstCustomObject> = {
  <cus_obj: Custom_Object> => {
    vec![cus_obj]
  },
  <cus_objs: With_Custom_Objects> (",")? <cus_obj: Custom_Object> => {
    let mut list = cus_objs;
    list.push(cus_obj);
    list
  },
}

Custom_Object: AstCustomObject = {
  "member" <for_meb_segs: Segs_Obj> "as" <exp: Expression> => {
    AstCustomObject::FormulaMember(for_meb_segs, exp)
  },
  "set" <cus_set_segs: Segs_Obj> "as" <ast_set: Brace_Set> => {
    AstCustomObject::CustomSet(cus_set_segs, ast_set)
  },
  "set" <cus_set_segs: Segs_Obj> "as" <segs_obj: Segs_Obj> => {
    AstCustomObject::CustomSet(cus_set_segs, AstSet::SegsObj(segs_obj))
  },
}

Expression: AstExpression = {
  <term: Term> => {
    AstExpression {
      terms: vec![('+', term)],
    }
  },
  <exp: Expression> "+" <term: Term> => {
    let mut exp = exp;
    exp.terms.push(('+', term));
    exp
  },
  <exp: Expression> "-" <term: Term> => {
    let mut exp = exp;
    exp.terms.push(('-', term));
    exp
  }
}

Term: AstTerm = {
  <factory: Factory> => {
    AstTerm {
      factories: vec![('*', factory)],
    }
  },
  <term: Term> "*" <factory: Factory> => {
    let mut term = term;
    term.factories.push(('*', factory));
    term
  },
  <term: Term> "/" <factory: Factory> => {
    let mut term = term;
    term.factories.push(('/', factory));
    term
  }
}

Factory: AstFactory = {
  <numeric: "double"> => {
    FactoryNum(numeric)
  },
  <long_int: "unsigned_int"> => {
    // let numeric: f64 = long_int as f64;
    FactoryNum(long_int as f64)
  },
  <str: "QuotedString"> => {
    FactoryStr(str)
  },
  <segs_obj: Segs_Obj> => {
    FactorySegs(segs_obj)
  },
  "(" <segs_objs: Segs_Objects> "," <segs_obj: Segs_Obj> ")" => {
    let mut segs_objs = segs_objs;
    segs_objs.push(segs_obj);
    FactoryTuple(AstTuple::SegsObjects(segs_objs))
  },
  "(" <exp: Expression> ")" => {
    FactoryExp(exp)
  },
}

Bracket_Tuple: AstTuple = {
  "(" <segs_objs: Segs_Objects> ")" => {
    AstTuple::SegsObjects(segs_objs)
  },
}

Brace_Set: AstSet = {
  "{" <tuples: Tuples> "}" => {
    AstSet::Tuples(tuples)
  },
}

Tuples: Vec<AstTuple> = {
  <tuple_segs: Segs_Obj> => {
    vec![AstTuple::SegsObj(tuple_segs)]
  },
  <bracket_tup: Bracket_Tuple> => {
    vec![bracket_tup]
  },
  <tuples: Tuples> "," <tuple_segs: Segs_Obj> => {
    let mut list = tuples;
    list.push(AstTuple::SegsObj(tuple_segs));
    list
  },
  <tuples: Tuples> "," <bracket_tup: Bracket_Tuple> => {
    let mut list = tuples;
    list.push(bracket_tup);
    list
  },
}

Segs_Objects: Vec<AstSegsObj> = {
  <segs_obj: Segs_Obj> => {
    vec![segs_obj]
  },
  <segs_objs: Segs_Objects> "," <segs_obj: Segs_Obj> => {
    let mut list = segs_objs;
    list.push(segs_obj);
    list
  },
}

Segs_Obj: AstSegsObj = {
  <seg: Seg> => {
    AstSegsObj::new(seg)
  },
  <segs: Segs_Obj> "." <seg: Seg> => {
    let mut segs = segs;
    segs.append(seg);
    segs
  },
}

Seg: AstSeg = {
  "&" <int: "unsigned_int"> => {
    AstSeg::Gid(int)
  },
  "&" <int: "unsigned_int"> <bra_str: "BracketedString"> => {
    AstSeg::GidStr(int, bra_str)
  },
  <bra_str: "BracketedString"> => {
    AstSeg::Str(bra_str)
  },
  <func: Member_Func> => {
    AstSeg::MemberFunc(func)
  },
  <func: Set_Func> => {
    AstSeg::SetFunc(func)
  },
  <func: Exp_Func> => {
    AstSeg::ExpFunc(func)
  },
  <func: Level_Func> => {
    AstSeg::LevelFunc(func)
  },
}

Member_Func: AstMemberFunction = {
  <memb_func: Parent> => {
    AstMemberFunction::Parent(memb_func)
  },
  <memb_func: Closing_Period> => {
    AstMemberFunction::ClosingPeriod(memb_func)
  },
  <memb_func: Opening_Period> => {
    AstMemberFunction::OpeningPeriod(memb_func)
  },
  <memb_func: Current_Member> => {
    AstMemberFunction::CurrentMember(memb_func)
  },
}

Parent: AstMemberFnParent = {
  "Parent" ("(" ")")? => {
    AstMemberFnParent::Chain
  },
  "Parent" "(" <mem_segs: Segs_Obj> ")" => {
    AstMemberFnParent::MemSegs(mem_segs)
  },
}

Closing_Period: AstMemberFnClosingPeriod = {
  "ClosingPeriod" ("(" ")")? => {
    AstMemberFnClosingPeriod::Chain
  },
  "ClosingPeriod" "(" <level_segs: Segs_Obj> ")" => {
    AstMemberFnClosingPeriod::LvSegs(level_segs)
  },
  "ClosingPeriod" "(" <level_segs: Segs_Obj> "," <member_segs: Segs_Obj> ")" => {
    AstMemberFnClosingPeriod::LvSegs_MemSegs(level_segs, member_segs)
  },
}

Opening_Period: AstMemberFnOpeningPeriod = {
  "OpeningPeriod" ("(" ")")? => {
    AstMemberFnOpeningPeriod::Chain
  },
  "OpeningPeriod" "(" <level_segs: Segs_Obj> ")" => {
    AstMemberFnOpeningPeriod::LvSegs(level_segs)
  },
  "OpeningPeriod" "(" <level_segs: Segs_Obj> "," <member_segs: Segs_Obj> ")" => {
    AstMemberFnOpeningPeriod::LvSegs_MemSegs(level_segs, member_segs)
  },
}

Current_Member: AstMemberFnCurrentMember = {
  "CurrentMember" ("(" ")")? => {
    AstMemberFnCurrentMember::Chain
  },
  "CurrentMember" "(" <segs_obj: Segs_Obj> ")" => {
    AstMemberFnCurrentMember::SegsObj(segs_obj)
  },
}

Level_Func: AstLevelFunction = {
  <lv_func: Level> => {
    AstLevelFunction::Level(lv_func)
  },
  <lv_func: Levels> => {
    AstLevelFunction::Levels(lv_func)
  },
}

Level: AstLevelFnLevel = {
  "Level" ("(" ")")? => {
    AstLevelFnLevel::Chain
  },
  "Level" "(" <mem_segs: Segs_Obj> ")" => {
    AstLevelFnLevel::MemSegs(mem_segs)
  },
}

Levels: AstLevelFnLevels = {
  "Levels" "(" <exp: Expression> ")" => {
    AstLevelFnLevels::Chain_Exp(exp)
  },
  "Levels" "(" <segs_obj: Segs_Obj> "," <exp: Expression> ")" => {
    AstLevelFnLevels::SegsObj_Exp(segs_obj, exp)
  },
}

Set_Func: AstSetFunction = {
  <set_func: Children> => {
    AstSetFunction::Children(set_func)
  },
}

Children: AstSetFnChildren = {
  "Children" ("(" ")")? => {
    AstSetFnChildren::Chain
  },
  "Children" "(" <mem_segs: Segs_Obj> ")" => {
    AstSetFnChildren::MemSegs(mem_segs)
  },
}

Exp_Func: AstExpFunction = {
  <num_func: Avg> => {
    AstExpFunction::Avg(num_func)
  },
  <num_func: Count> => {
    AstExpFunction::Count(num_func)
  },
  <num_func: IIf> => {
    AstExpFunction::IIf(num_func)
  },
  <num_func: Sum> => {
    AstExpFunction::Sum(num_func)
  },
  <num_func: Max> => {
    AstExpFunction::Max(num_func)
  },
  <num_func: Min> => {
    AstExpFunction::Min(num_func)
  },

  <exp_func: LookupCube> => {
    AstExpFunction::LookupCube(exp_func)
  },

  <str_func: Name> => {
    AstExpFunction::Name(str_func)
  },
}

Count: AstNumFnCount = {
  "Count" ("(" ")")? => {
    AstNumFnCount::Chain
  },
  "Count" "(" <ast_set: Brace_Set> ")" => {
    AstNumFnCount::AstSet(ast_set)
  },
  "Count" "(" <set_segs: Segs_Obj> ")" => {
    AstNumFnCount::AstSet(AstSet::SegsObj(set_segs))
  },
}

Avg: AstNumFnAvg = {
  "Avg" ("(" ")")? => {
    AstNumFnAvg::Chain
  },
  // "Avg" "(" <exp: Expression> ")" => {
  //   AstNumFnAvg::Chain_Exp(exp)
  // },
  "Avg" "(" <set_segs: Segs_Obj> ")" => {
    AstNumFnAvg::AstSet(AstSet::SegsObj(set_segs))
  },
  "Avg" "(" <ast_set: Brace_Set> ")" => {
    AstNumFnAvg::AstSet(ast_set)
  },
  "Avg" "(" <set_segs: Segs_Obj> "," <exp: Expression> ")" => {
    AstNumFnAvg::AstSet_Exp(AstSet::SegsObj(set_segs), exp)
  },
  "Avg" "(" <ast_set: Brace_Set> "," <exp: Expression> ")" => {
    AstNumFnAvg::AstSet_Exp(ast_set, exp)
  },
}

Sum: AstNumFnSum = {
  "Sum" ("(" ")")? => {
    AstNumFnSum::Chain
  },
  // "Sum" "(" <exp: Expression> ")" => {
  //   AstNumFnSum::Chain_Exp(exp)
  // },
  "Sum" "(" <set_segs: Segs_Obj> ")" => {
    AstNumFnSum::AstSet(AstSet::SegsObj(set_segs))
  },
  "Sum" "(" <ast_set: Brace_Set> ")" => {
    AstNumFnSum::AstSet(ast_set)
  },
  "Sum" "(" <set_segs: Segs_Obj> "," <exp: Expression> ")" => {
    AstNumFnSum::AstSet_Exp(AstSet::SegsObj(set_segs), exp)
  },
  "Sum" "(" <ast_set: Brace_Set> "," <exp: Expression> ")" => {
    AstNumFnSum::AstSet_Exp(ast_set, exp)
  },
}

Max: AstNumFnMax = {
  "Max" ("(" ")")? => {
    AstNumFnMax::Chain
  },
  // "Max" "(" <exp: Expression> ")" => {
  //   AstNumFnMax::Chain_Exp(exp)
  // },
  "Max" "(" <set_segs: Segs_Obj> ")" => {
    AstNumFnMax::AstSet(AstSet::SegsObj(set_segs))
  },
  "Max" "(" <ast_set: Brace_Set> ")" => {
    AstNumFnMax::AstSet(ast_set)
  },
  "Max" "(" <set_segs: Segs_Obj> "," <exp: Expression> ")" => {
    AstNumFnMax::AstSet_Exp(AstSet::SegsObj(set_segs), exp)
  },
  "Max" "(" <ast_set: Brace_Set> "," <exp: Expression> ")" => {
    AstNumFnMax::AstSet_Exp(ast_set, exp)
  },
}

Min: AstNumFnMin = {
  "Min" ("(" ")")? => {
    AstNumFnMin::Chain
  },
  // "Min" "(" <exp: Expression> ")" => {
  //   AstNumFnMin::Chain_Exp(exp)
  // },
  "Min" "(" <set_segs: Segs_Obj> ")" => {
    AstNumFnMin::AstSet(AstSet::SegsObj(set_segs))
  },
  "Min" "(" <ast_set: Brace_Set> ")" => {
    AstNumFnMin::AstSet(ast_set)
  },
  "Min" "(" <set_segs: Segs_Obj> "," <exp: Expression> ")" => {
    AstNumFnMin::AstSet_Exp(AstSet::SegsObj(set_segs), exp)
  },
  "Min" "(" <ast_set: Brace_Set> "," <exp: Expression> ")" => {
    AstNumFnMin::AstSet_Exp(ast_set, exp)
  },
}

IIf: AstNumFnIIf = {
  "IIf" "(" <bool_exp: BoolExpression> "," <true_exp: Expression> "," <false_exp: Expression> ")" => {
    AstNumFnIIf { bool_exp, true_exp, false_exp }
  }
}

AxisPos: u64 = {
  <axpos: "unsigned_int"> => axpos,
  "Columns"     => 0,
  "Rows"        => 1,
  "Pages"       => 2,
  "Chapters"    => 3,
  "Sections"    => 4,
}

// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<<<<<<
// code review mark <<<<<<<<<<
// code review mark <<<<<

LookupCube: AstExpFnLookupCube = {
    "LookupCube" "(" <cube_segs: Segs_Obj> "," <exp_on_another_cube: Expression> ")" => {
      AstExpFnLookupCube::new(Some(cube_segs), exp_on_another_cube)
    },
    "LookupCube" "(" <exp_on_another_cube: Expression> ")" => {
      AstExpFnLookupCube::new(None, exp_on_another_cube)
    },
}

Name: AstExpFnName = {
  "Name" => {
    AstExpFnName::NoParam
  },
  "Name" "(" ")" => {
    AstExpFnName::NoParam
  },
  "Name" "(" <olap_obj_segs: Segs_Obj> ")" => {
    AstExpFnName::InnerParam(olap_obj_segs)
  },
}

BoolExpression: AstBoolExp = {
  <bt: BoolTerm> => {
    AstBoolExp::BoolTerm(bt)
  },
  "Not" <bt: BoolTerm> => {
    AstBoolExp::NotBoolTerm(bt)
  },
  <bool_exp: BoolExpression> "Or" <bt: BoolTerm> => {
    AstBoolExp::BoolExpOrBoolTerm(Box::new(bool_exp), bt)
  }
}

BoolTerm: AstBoolTerm = {
  <bf: BoolFactory> => {
    AstBoolTerm::BoolFactory(bf)
  },
  <bt: BoolTerm> "And" <bf: BoolFactory> => {
    AstBoolTerm::BoolTermAndBoolFactory(Box::new(bt), bf)
  }
}

BoolFactory: AstBoolFactory = {
  <exp_left: Expression> "<" <exp_right: Expression> => {
    AstBoolFactory::ExpressionComparesAnother(exp_left, String::from("<"), exp_right)
  },
  <exp_left: Expression> "<=" <exp_right: Expression> => {
    AstBoolFactory::ExpressionComparesAnother(exp_left, String::from("<="), exp_right)
  },
  <exp_left: Expression> "=" <exp_right: Expression> => {
    AstBoolFactory::ExpressionComparesAnother(exp_left, String::from("="), exp_right)
  },
  <exp_left: Expression> "<>" <exp_right: Expression> => {
    AstBoolFactory::ExpressionComparesAnother(exp_left, String::from("<>"), exp_right)
  },
  <exp_left: Expression> ">" <exp_right: Expression> => {
    AstBoolFactory::ExpressionComparesAnother(exp_left, String::from(">"), exp_right)
  },
  <exp_left: Expression> ">=" <exp_right: Expression> => {
    AstBoolFactory::ExpressionComparesAnother(exp_left, String::from(">="), exp_right)
  },
  "(" <bool_exp: BoolExpression> ")" => {
    AstBoolFactory::BoolExp(Box::new(bool_exp))
  },
  <bool_fn: BoolFunction> => {
    AstBoolFactory::BoolFn(bool_fn)
  }
}

BoolFunction: AstBoolFunction = {
  <is_leaf: BoolFnIsLeaf> => {
    AstBoolFunction::IsLeaf(is_leaf)
  }
}

BoolFnIsLeaf: AstBoolFnIsLeaf = {
  // "IsLeaf" => {
  //   AstBoolFnIsLeaf::new(None)
  // },
  // "IsLeaf" "(" ")" => {
  //   AstBoolFnIsLeaf::new(None)
  // },
  "IsLeaf" "(" <member_segs: Segs_Obj> ")" => {
    AstBoolFnIsLeaf::new(member_segs)
  },
}
